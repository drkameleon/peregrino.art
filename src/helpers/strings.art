;===============================================
; Peregrino.art
;
; Lightning-fast command-line 
; benchmarking tool & library
; for Arturo
;
; MIT License
; (c) 2026 Yanis ZafirÃ³pulos
;-----------------------------------------------
; @file src/helpers/strings.art
;===============================================

fillEvenly: function [text,headstart][
    words: split.words text
    lines: [[]]
    loop words 'word [
        if (2 + headstart) > terminal\width - ((size join.with:" " last lines) + (size word) + 1)[
            lines: lines ++ @[[]]
        ]
        lines\[dec size lines]: lines\[dec size lines] ++ word
    ]
    join.with: "\n" ++ repeat " " headstart map lines 'line [
        join.with: " " line
    ]
]

nicefloat: function [num][
    to :string .format:".2f" round.to: 2 to :floating num
]

truncateLeft: function [str,lim][
    if lim >= size str -> return str
    pref: "..."
    return pref ++ last.n: min @[lim - size pref, (size str) - size pref] str
]

spacer: function [len][
    repeat " " len
]